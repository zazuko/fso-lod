PREFIX gont: <https://gont.ch/>

CONSTRUCT {
  <http://classifications.data.admin.ch/municipality/1> gont:populationSegment [
    gont:fromAge ?fromAge ;
    gont:toAge ?toAge ;
    gont:malePopulation ?Male_Inhabitants ;
    gont:femalePopulation ?Female_Inhabitants 
  ]
}

WHERE
{
  {
    SELECT 
      (?ageinterval AS ?fromAge)
      (?ageinterval+4 AS ?toAge)
      (SUM(IF(?sex=<http://data.admin.ch/bfs/code/1.0/CL_SEX/2>,?number,0)) AS ?Female_Inhabitants)
      (SUM(IF(?sex=<http://data.admin.ch/bfs/code/1.0/CL_SEX/1>,?number,0)) AS ?Male_Inhabitants)
    WHERE
    {
        ?s <http://data.admin.ch/bfs/property/NB_PERSON> ?number;
        <http://data.admin.ch/bfs/property/POPULATIONTYPE> ?ptype;
        <http://data.admin.ch/bfs/property/SEX> ?sex;
        <http://data.admin.ch/bfs/property/REPORTINGMUNICIPALITYID> ?remuniuri;
        <http://data.admin.ch/bfs/property/AGE> ?age.

        ?remuniuri <http://www.w3.org/2004/02/skos/core#notation> "1".
        ?age <http://www.w3.org/2004/02/skos/core#notation> ?agenumber.

        ?ptype <http://www.w3.org/2004/02/skos/core#notation> ?pnumber.
        FILTER ((xsd:int(?pnumber)) <= 2)

      }
      GROUP BY ((FLOOR((xsd:int(?agenumber))/5)*5) AS ?ageinterval)
      ORDER BY DESC(xsd:int(?ageinterval))
  }
}
