PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gont: <https://gont.ch/> 

INSERT { GRAPH <http://example.org/inference> {
   ?munuri rdfs:seeAlso ?dbpuri ;
}}
WHERE
{ GRAPH <http://example.org/fso> {
   SERVICE <http://dbpedia.org/sparql>
   {
      ?dbpuri dbo:country dbr:Switzerland ;
        dbo:municipalityCode ?munid .
      
      # apparently also a string now
      BIND (xsd:integer(?munid) AS ?municipalityId )
   }

  ?munuri a gont:Municipality ;
    gont:id ?municipalityId .

}}